*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        enable_tenant
Test Template     Enable Tenant Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TenantID          Version
Enable tenant for valid ID and version
                      [Tags]    main_scenarios
                      OK        ${Tenant_ID}      5

Enable tenant without version
                      OK        ${Tenant_ID}      ${EMPTY}

Enable tenant for invalid version
                      OK        ${Tenant_ID}      Invalid

Enable tenant for invalid Tenant ID
                      NOK       Invalid           7

Enable tenant details without Tenant ID
                      NOK       ${EMPTY}          7

Enable tenant for old version
                      NOK       ${Tenant_ID}      1

*** Keywords ***
Startup
    Log    *******Start Test "ENABLE TENANT"*******
    Log    *******Connect to AE*******
    Connect AE

Enable tenant Template
    [Arguments]    ${OK_NOK}    ${TenantID}    ${Version}
    [Documentation]    This action enables tenant on Acronis.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TenantID}: Provide the tenant id that you want to enable.
    ...    -${Version}: Provide the tenant version.
    Generate String
    Init Action    ${ENABLE_TENANT}
    Connect to Acronis
    Run Keyword If    '${TenantID}' == 'Invalid'    Action Set    &UC4RB_AC_TENANT_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_TENANT_ID#    ${TenantID}
    Run Keyword If    '${Version}' == 'Invalid'    Action Set    &UC4RB_AC_TENANT_VERSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_TENANT_VERSION#    ${Version}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "ENABLE TENANT"*******
