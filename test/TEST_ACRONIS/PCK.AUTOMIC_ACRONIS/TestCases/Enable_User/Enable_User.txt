*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        enable_user
Test Template     Enable User Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    UserID            Version
Enable user details for valid ID and current version
                      [Tags]    main_scenarios
                      OK        ${Gen_UserID}     ${Gen_Version}

Enable user details for valid ID but without current version
                      OK        ${Gen_UserID}     ${EMPTY}

Enable user details for valid ID but invalid current version
                      NOK       ${Gen_UserID}     Invalid

Enable user details for valid ID but old version
                      NOK       ${Gen_UserID}     1

Enable user details for invalid ID
                      NOK       Invalid           ${Gen_Version}

Enable user details without ID
                      NOK       ${EMPTY}          ${Gen_Version}

*** Keywords ***
Startup
    Log    *******Start Test "ENABLE USER"*******
    Log    *******Connect to AE*******
    Connect AE
    Generate Number
    Generate String
    Create User    ${CREATE_USER_ACTION}    ${Parent_TenantID}    ${Generated_Name}    ${Generated_Name}@broadcom.com    ${EMPTY}    ${EMPTY}

Enable User Template
    [Arguments]    ${OK_NOK}    ${UserID}    ${Version}
    [Documentation]    This action enables a user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${UserID}: Provide the user id that you want to enable.
    ...    -${Version}: Provide the current version of the user.
    Generate String
    Init Action    ${ENABLE_USER_ACTION}
    Connect to Acronis
    Run Keyword If    '${UserID}' == 'Invalid'    Action Set    &UC4RB_AC_USER_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_USER_ID#    ${UserID}
    Run Keyword If    '${Version}' == 'Invalid'    Action Set    &UC4RB_AC_USER_VERSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_USER_VERSION#    ${Version}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AC_NEW_USER_VERSION#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AC_NEW_USER_VERSION#

Teardown
    Delete User    ${DELETE_USER_ACTION}    ${Gen_UserID}    true
    Log    *******End Test "ENABLE USER"*******
