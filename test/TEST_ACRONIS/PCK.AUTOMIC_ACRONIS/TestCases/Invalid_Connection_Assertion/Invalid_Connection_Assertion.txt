*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        invalid_connection
Test Template     Invalid Connection Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Endpoint                       Version               Username               Password
Assertion with Invalid Endpoint
                      [Tags]    Invalid Connectio Scenarios
                      NOK       Invalid                        ${Acronis_Version}    ${Acronis_Username}    ${Acronis_Password}

Assertion with Invalid Version
                      NOK       ${Acronis_URL}                 Invalid               ${Acronis_Username}    ${Acronis_Password}

Assertion with Invalid Username
                      NOK       ${Acronis_URL}                 ${Acronis_Version}    Invalid                ${Acronis_Password}

Assertion with Invalid Password
                      NOK       ${Acronis_URL}                 ${Acronis_Version}    ${Acronis_Username}    Invalid

*** Keywords ***
Startup
    Log    *******Start Test "INVALID CONNECTION"*******
    Log    *******Connect to AE*******
    Connect AE

Invalid Connection Template
    [Arguments]    ${OK_NOK}    ${Endpoint}    ${Version}    ${Username}    ${Password}
    [Documentation]    This action assert action behavior when invalid connection details.
    Init Action    ${GET_USER}
    Generate String
    Run Keyword If    '${Endpoint}' == 'Invalid'    Action Set    &UC4RB_AC_ENDPOINT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_ENDPOINT#    ${Endpoint}
    Run Keyword If    '${Version}' == 'Invalid'    Action Set    &UC4RB_AC_API_VERSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_API_VERSION#    ${Version}
    Run Keyword If    '${Username}' == 'Invalid'    Action Set    &UC4RB_AC_USERNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_USERNAME#    ${Username}
    Run Keyword If    '${Password}' == 'Invalid'    Action Set    &UC4RB_AC_PASSWORD#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_PASSWORD#    ${Password}
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "INVALID CONNECTION"*******
