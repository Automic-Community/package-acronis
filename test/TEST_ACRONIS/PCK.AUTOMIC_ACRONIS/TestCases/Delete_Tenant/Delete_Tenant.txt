*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_tenant
Test Template     Delete Tenant Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TenantID               FailIfNonExisting
Delete tenant for existing tenant id and fail if non existing is set as true
                      [Tags]    main_scenarios
                      OK        ${Gen_Tenant_ID}       true

Delete a tenant for an non-existing tenant ID and fail if non existing is set as false
                      OK        ${Generated_Number}    false

Delete a tenant for an non-existing tenant ID and fail if non existing is set as true
                      NOK       ${Generated_Number}    true

*** Keywords ***
Startup
    Log    *******Start Test "DELETE TENANT"*******
    Log    *******Connect to AE*******
    Connect AE
    Generate String
    Generate Number
    Create Tenant    ${CREATE_TENANT_ACTION}    ${Generated_Name}    ${Parent_TenantID}    Customer    ${Generated_Name}@broadcom.com    ${FirstName}
    ...    ${LastName}

Delete Tenant Template
    [Arguments]    ${OK_NOK}    ${TenantID}    ${FailIfNonExisting}
    [Documentation]    This action deletes an existing tenant from Acronis.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TenantID}: Provide the tenant id to be deleted.
    ...    -${FailIfNonExisting}: Select true if you want to fail action in case of non-existing id.
    Init Action    ${DELETE_TENANT_ACTION}
    Connect to Acronis
    Run Keyword If    '${TenantID}' == 'Invalid'    Action Set    &UC4RB_AC_TENANT_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_TENANT_ID#    ${TenantID}
    Action Set    &UC4RB_AC_FAIL#    ${FailIfNonExisting}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "DELETE TENANT"*******
