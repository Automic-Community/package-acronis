*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        switch_customer_to_production
Test Template     Switch Customer to Production Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TenantID
Switch customer toproduction mode for valid Tenant ID
                      [Tags]    main_scenarios
                      OK        ${Gen_Tenant_ID}

Switch customer toproduction mode for invalid Tenant ID
                      NOK       Invalid

Switch customer toproduction mode without Tenant ID
                      NOK       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "SWITCH CUSTOMER TO PRODUCTION"*******
    Log    *******Connect to AE*******
    Connect AE
    Generate String
    Generate Number
    Create Tenant    ${CREATE_TENANT_ACTION}    ${Generated_Name}    ${Parent_TenantID}    Customer    ${Generated_Name}@broadcom.com    ${FirstName}
    ...    ${LastName}

Switch Customer to Production Template
    [Arguments]    ${OK_NOK}    ${TenantID}
    [Documentation]    This action switches tenant mode from customer to production.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TenantID}: Provide the tenant ID.
    Generate String
    Init Action    ${SWITCH_MODE_ACTION}
    Connect to Acronis
    Run Keyword If    '${TenantID}' == 'Invalid'    Action Set    &UC4RB_AC_TENANT_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_TENANT_ID#    ${TenantID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AC_TENANT_MODE#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AC_TENANT_MODE#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_AC_TENANT_NEW_VERSION#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_AC_TENANT_NEW_VERSION#

Teardown
    Delete Tenant    ${DELETE_TENANT_ACTION}    ${Gen_Tenant_ID}    true
    Log    *******End Test "SWITCH CUSTOMER TO PRODUCTION"*******
