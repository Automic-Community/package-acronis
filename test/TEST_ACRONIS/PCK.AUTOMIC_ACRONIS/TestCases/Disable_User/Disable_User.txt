*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        disable_tenant
Test Template     Disable Tenant Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TenantID          Version
Disable tenant for valid ID and version
                      [Tags]    main_scenarios
                      OK        ${Tenant_ID}      3

Disable tenant for invalid ID
                      NOK       Invalid           3

Disable tenant details without ID
                      NOK       ${EMPTY}          3

Disable tenant for version not current
                      NOK       ${Tenant_ID}      1

Disable tenant for invalid version
                      NOK       ${Tenant_ID}      Invalid

Disable tenant without version
                      NOK       ${Tenant_ID}      ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "DISABLE TENANT"*******
    Log    *******Connect to AE*******
    Connect AE

Disable tenant Template
    [Arguments]    ${OK_NOK}    ${TenantID}    ${Version}
    [Documentation]    This action disables tenant on Acronis.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${TenantID}: Provide the tenant id that you want to disable.
    ...    -${Version}: Provide the tenant version.
    Generate String
    Init Action    ${DISABLE_TENANT}
    Connect to Acronis
    Run Keyword If    '${TenantID}' == 'Invalid'    Action Set    &UC4RB_AC_TENANT_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_TENANT_ID#    ${TenantID}
    Run Keyword If    '${Version}' == 'Invalid'    Action Set    &UC4RB_AC_TENANT_VERSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_TENANT_VERSION#    ${Version}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "DISABLE TENANT"*******
