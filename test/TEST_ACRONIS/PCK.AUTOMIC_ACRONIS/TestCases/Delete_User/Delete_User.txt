*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        delete_user
Test Template     Delete User Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    UserID            Version
Delete user for valid ID and version
                      [Tags]    main_scenarios
                      OK        ${User_ID}        ${User_Version}

Delete user for invalid ID
                      NOK       Invalid           ${User_Version}

Delete user details without ID
                      NOK       ${EMPTY}          ${User_Version}

Delete user for invalid version
                      NOK       ${User_ID}        Invalid

Delete user without version
                      NOK       ${User_ID}        ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "DELETE USER"*******
    Log    *******Connect to AE*******
    Connect AE

Delete User Template
    [Arguments]    ${OK_NOK}    ${UserID}    ${Version}
    [Documentation]    This action fetches a user details from Acronis.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${UserID}: Provide the user id that you want to fetch details.
    ...    -${Version}: Provide the user version.
    Generate String
    Init Action    ${DELETE_USER}
    Connect to Acronis
    Run Keyword If    '${UserID}' == 'Invalid'    Action Set    &UC4RB_AC_USER_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_USER_ID#    ${UserID}
    Run Keyword If    '${Version}' == 'Invalid'    Action Set    &UC4RB_AC_USER_VERSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_USER_VERSION#    ${Version}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "DELETE USER"*******
