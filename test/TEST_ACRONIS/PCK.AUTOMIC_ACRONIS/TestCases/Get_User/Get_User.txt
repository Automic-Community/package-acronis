*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_user
Test Template     Get User Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    UserID
Get user details for valid ID
                      [Tags]    main_scenarios
                      OK        ${Gen_UserID}

Get user details for invalid ID
                      NOK       Invalid

Get user details without ID
                      NOK       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "GET USER"*******
    Log    *******Connect to AE*******
    Connect AE
    Generate String
    Create User    ${CREATE_USER_ACTION}    ${Parent_TenantID}    ${Generated_Name}    ${Generated_Name}@broadcom.com    ${EMPTY}    ${EMPTY}

Get User Template
    [Arguments]    ${OK_NOK}    ${UserID}
    [Documentation]    This action fetches a user details from Acronis.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${UserID}: Provide the user id that you want to fetch details.
    Generate String
    Init Action    ${GET_USER}
    Connect to Acronis
    Run Keyword If    '${UserID}' == 'Invalid'    Action Set    &UC4RB_AC_USER_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_AC_USER_ID#    ${UserID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Delete User    ${DELETE_USER_ACTION}    ${Gen_UserID}    true
    Log    *******End Test "GET USER"*******
